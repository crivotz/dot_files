#########################################################################
# FIRST PROMPT LINE
#########################################################################
rosso='\e[1;34m'
NC='\e[0m'
echo -e "${rosso}Debian${NC}" `cat /etc/debian_version` "| ${rosso}ZSH${NC} ${ZSH_VERSION}"
#########################################################################
# TMUX
#########################################################################
if [[ ! $(tmux ls) ]] 2> /dev/null; then 
  tmux new -s λ
fi
#########################################################################
# TMUX
#########################################################################
local extract="
local in=\${\${\"\$(<{f})\"%\$'\0'*}#*\$'\0'}
local -A ctxt=(\"\${(@ps:\2:)CTXT}\")
local realpath=\${ctxt[IPREFIX]}\${ctxt[hpre]}\$in
realpath=\${(Qe)~realpath}
"
#########################################################################
# CHPWD
#########################################################################
chpwd() {
  set -- "$(git rev-parse --show-toplevel 2> /dev/null)"
  # If cd'ing into a git working copy and not within the same working copy
  if [ -n "$1" ] && [ "$1" != "$vc_root" ]; then
    vc_root="$1"
    git fetch
  fi
  # ls inside folder after cd
  eza --git --icons --classify --group-directories-first --time-style=long-iso --group --color-scale
}
#########################################################################
# P10K INSTANT PROMPT
#########################################################################
# Enable Powerlevel10k instant prompt. Should stay close to the top of ~/.zshrc.
# Initialization code that may require console input (password prompts, [y/n]
# confirmations, etc.) must go above this block; everything else may go below.
if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi
#########################################################################
# ZINIT
#########################################################################
### Added by Zinit's installer
if [[ ! -f $HOME/.zinit/bin/zinit.zsh ]]; then
  print -P "%F{33}▓▒░ %F{220}Installing DHARMA Initiative Plugin Manager (zdharma-continuum/zinit)…%f"
  command mkdir -p $HOME/.zinit
  command git clone https://github.com/zdharma-continuum/zinit $HOME/.zinit/bin && \
  print -P "%F{33}▓▒░ %F{34}Installation successful.%F" || \
  print -P "%F{160}▓▒░ The clone has failed.%F"
fi
source "$HOME/.zinit/bin/zinit.zsh"
autoload -Uz _zinit
(( ${+_comps} )) && _comps[zinit]=_zinit
### End of Zinit installer's chunk
#########################################################################
# THEME
#########################################################################
# P10K
zinit ice depth=1; zinit light romkatv/powerlevel10k
# STARSHIP
# zinit ice from"gh-r" as"command" atload'eval "$(starship init zsh)"'
# zinit load starship/starship
#########################################################################
# PLUGINS
#########################################################################
# SSH-AGENT
# zinit light bobsoppe/zsh-ssh-agent
# FZF
zinit ice from="gh-r" as="command" bpick="*linux_amd64*"
zinit light junegunn/fzf
# FZF BYNARY AND TMUX HELPER SCRIPT
zinit ice lucid wait'0c' as="command" id-as="junegunn/fzf-tmux" pick="bin/fzf-tmux"
zinit light junegunn/fzf
# BIND MULTIPLE WIDGETS USING FZF
zinit ice lucid wait'0c' multisrc"shell/{completion,key-bindings}.zsh" id-as="junegunn/fzf_completions" pick="/dev/null"
zinit light junegunn/fzf
# FZF-TAB
zinit ice wait="1" lucid
zinit light Aloxaf/fzf-tab
# AUTOSUGGESTIONS, TRIGGER PRECMD HOOK UPON LOAD
ZSH_AUTOSUGGEST_BUFFER_MAX_SIZE=20
zinit ice wait="2" lucid atload="_zsh_autosuggest_start"
zinit light zsh-users/zsh-autosuggestions
# ENHANCD
zinit ice wait="0b" lucid
zinit light b4b4r07/enhancd
export ENHANCD_FILTER=fzf:fzy:peco
# ZOXIDE
zinit ice wait="0" lucid from="gh-r" as="program" pick="zoxide-*/zoxide -> zoxide" cp="zoxide-*/completions/_zoxide -> _zoxide" atclone="./zoxide init zsh > init.zsh" atpull="%atclone" src="init.zsh"
zinit light ajeetdsouza/zoxide
# TAB COMPLETIONS
zinit ice wait="0b" lucid blockf
zinit light zsh-users/zsh-completions
zstyle ':completion:*' completer _expand _complete _ignored _approximate
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Z}'
zstyle ':completion:*' menu no
zstyle ':completion:*' select-prompt '%SScrolling active: current selection at %p%s'
zstyle ':completion:*:descriptions' format '[%d]'
zstyle ':completion:*:processes' command 'ps -au$USER'
zstyle ':completion:complete:*:options' sort false
zstyle ':fzf-tab:complete:_zlua:*' query-string input
zstyle ':completion:*:*:*:*:processes' command "ps -u $USER -o pid,user,comm,cmd -w -w"
zstyle ':fzf-tab:complete:kill:argument-rest' extra-opts --preview=$extract'ps --pid=$in[(w)1] -o cmd --no-headers -w -w' --preview-window=down:3:wrap
zstyle ':fzf-tab:complete:cd:*' fzf-preview 'eza -1 --color=always $realpath'
zstyle ':fzf-tab:*' use-fzf-default-opts yes
zstyle ":completion:*:git-checkout:*" sort false
zstyle ':completion:*' list-colors ${(s.:.)LS_COLORS}
# SAD
zinit ice from="gh-r" as="command"
zinit light ms-jpq/sad
# SYNTAX HIGHLIGHTING
zinit ice wait="2" lucid atinit="zpcompinit;zpcdreplay"
zinit light zdharma-continuum/fast-syntax-highlighting
# EZA
zinit ice wait="2" lucid from="gh-r" as="program"
zinit light eza-community/eza
zinit ice wait blockf atpull'zinit creinstall -q .'
# DELTA
zinit ice lucid wait="0" as="program" from="gh-r" bpick="*amd64.deb" pick="usr/bin/delta"
zinit light dandavison/delta
# BTOP
zinit ice wait="2" lucid from="gh-r" as="program" pick="btop/bin/btop"
zinit light aristocratos/btop
# DUF
zinit ice lucid wait="0" as="program" from="gh-r" bpick='*linux_amd64.deb' pick="usr/bin/duf" atload="alias df=duf"
zinit light muesli/duf
# BAT
zinit ice from="gh-r" as="program" pick="usr/bin/bat" bpick="*amd64.deb" atload="alias cat=bat"
zinit light sharkdp/bat
# BAT-EXTRAS
zinit ice lucid wait="1" as="program" pick="src/batgrep.sh"
zinit ice lucid wait="1" as="program" pick="src/batdiff.sh"
zinit light eth-p/bat-extras
alias rg=batgrep.sh
alias bd=batdiff.sh
alias man=batman.sh
# RIPGREP
zinit ice from="gh-r" as="program" bpick="*amd64.deb" pick="usr/bin/rg"
zinit light BurntSushi/ripgrep
# NEOVIM
zinit ice from="gh-r" as="program" bpick="*linux-x86_64.tar.gz" ver="nightly" pick="nvim-linux-x86_64/bin/nvim"
zinit light neovim/neovim
# LAZYGIT
zinit ice lucid wait="0" as="program" from="gh-r" bpick="*Linux_x86_64*" pick="lazygit" atload="alias lg='lazygit'"
zinit light jesseduffield/lazygit
# LAZYDOCKER
zinit ice lucid wait="0" as="program" from="gh-r" bpick="*Linux_x86_64*" pick="lazydocker" atload="alias ld='lazydocker'"
zinit light jesseduffield/lazydocker
# FD
zinit ice as="command" from="gh-r" bpick="*amd64.deb" pick="usr/bin/fd" 
zinit light sharkdp/fd
# GH-CLI
zinit ice lucid as="command" from="gh-r" bpick="*linux_amd64.deb" atclone="./gh completion -s zsh > _gh" atpull="%atclone" mv="**/bin/gh* -> gh" pick="usr/bin/gh"
zinit light cli/cli
# 1PASSWORD COMPLETION
zinit ice as="completion" atclone="command -v op &>/dev/null && op completion zsh > _op || true" atpull="zinit creinstall -q ."  nocompile
zinit light zdharma-continuum/null
# TREE-SITTER
zinit ice as="program" from="gh-r" mv="tree* -> tree-sitter" pick="tree-sitter"
zinit light tree-sitter/tree-sitter
# PRETTYPING
zinit ice lucid wait="" as="program" pick="prettyping" atload="alias ping=prettyping"
zinit load denilsonsa/prettyping
# GPING wait for glibc 2.39
zinit ice lucid wait="0" as="program" from="gh-r"
zinit load orf/gping
# ASDF
# asdf plugin add ruby https://github.com/asdf-vm/asdf-ruby.git
# asdf plugin add nodejs https://github.com/asdf-vm/asdf-nodejs.git
# asdf plugin add zig https://github.com/asdf-community/asdf-zig
zinit ice lucid wait="0" as="program" from="gh-r"
zinit light asdf-vm/asdf
# CROC
zinit ice lucid wait="0" as="program" from="gh-r" bpick="*64bit*"
zinit light schollz/croc
# ATUIN
zinit ice as"program" from"gh-r" pick="**/atuin" ver="v18.2.0"
zinit light atuinsh/atuin
# YAZI
zinit ice lucid wait"0" as"program" from"gh-r" bpick="*mus*" pick="*/yazi"
zinit light sxyazi/yazi
# ZSH-VI-MODE
zinit light jeffreytse/zsh-vi-mode
# OVERMIND
zinit ice lucid wait"0" as"program" from"gh-r" mv="over* -> overmind" pick="overmind"
zinit light DarthSim/overmind
# DIFFTASTIC
zinit ice lucid wait"0" as"program" from"gh-r"
zinit light Wilfred/difftastic
#########################################################################
# HISTORY
#########################################################################
[ -z "$HISTFILE" ] && HISTFILE="$HOME/.zhistory"
HISTSIZE=290000
SAVEHIST=$HISTSIZE
#########################################################################
# SETOPT
#########################################################################
setopt extended_history       # record timestamp of command in HISTFILE
setopt hist_expire_dups_first # delete duplicates first when HISTFILE size exceeds HISTSIZE
setopt hist_ignore_all_dups   # ignore duplicated commands history list
setopt hist_ignore_space      # ignore commands that start with space
setopt hist_verify            # show command with history expansion to user before running it
setopt inc_append_history     # add commands to HISTFILE in order of execution
setopt share_history          # share command history data
setopt always_to_end          # cursor moved to the end in full completion
setopt hash_list_all          # hash everything before completion
# setopt completealiases        # complete alisases
setopt always_to_end          # when completing from the middle of a word, move the cursor to the end of the word
setopt complete_in_word       # allow completion from within a word/phrase
setopt nocorrect                # spelling correction for commands
setopt list_ambiguous         # complete as much of a completion until it gets ambiguous.
setopt nolisttypes
setopt listpacked
setopt automenu
unsetopt BEEP
setopt vi
#########################################################################
# ENV VARIABLE
#########################################################################
export EDITOR='nvim'
export VISUAL=$EDITOR
export PAGER='less'
export SHELL='/bin/zsh'
export LANG='it_IT.UTF-8'
export LC_ALL='it_IT.UTF-8'
# export BAT_THEME="gruvbox-dark"
#########################################################################
# COLORING
#########################################################################
autoload colors && colors
#########################################################################
# ALIASES
#########################################################################
source $HOME/.zsh_aliases
#########################################################################
# env
#########################################################################
source $HOME/.nubem_env
#########################################################################
# YAZI
#########################################################################
function ya() {
  local tmp="$(mktemp -t "yazi-cwd.XXXXX")"
  yazi "$@" --cwd-file="$tmp"
  if cwd="$(cat -- "$tmp")" && [ -n "$cwd" ] && [ "$cwd" != "$PWD" ]; then
    cd -- "$cwd"
  fi
  rm -f -- "$tmp"
}
#########################################################################
# FANCY-CTRL-Z
#########################################################################
function fg-fzf() {
  job="$(jobs | fzf -0 -1 | sed -E 's/\[(.+)\].*/\1/')" && echo '' && fg %$job
}

function fancy-ctrl-z () {
  if [[ $#BUFFER -eq 0 ]]; then
    BUFFER=" fg-fzf"
    zle accept-line -w
  else
    zle push-input -w
    zle clear-screen -w
  fi
}
zle -N fancy-ctrl-z
bindkey '^Z' fancy-ctrl-z
#########################################################################
# RAILS VERSION
#########################################################################
function prompt_my_rails() {
  # Check if we're in a git repository
  if git rev-parse --is-inside-work-tree >/dev/null 2>&1; then
    # Get the root of the git repository
    local repo_root=$(git rev-parse --show-toplevel 2>/dev/null)
    
    # Check if Gemfile exists in the repository root
    if [[ -f "${repo_root}/Gemfile" ]]; then
      # Check if Rails is installed and available
      if command -v rails >/dev/null 2>&1; then
        # Get Rails version using rails -v and extract only the version number
        local rails_version=$(rails -v | cut -d' ' -f2)
        # Nerd Font Rails icon ( )
        p10k segment -s RAILS -f red -i "" -t "${rails_version}"
      fi
    fi
  fi
}
#########################################################################
# FZF SETTINGS
#########################################################################
export FZF_DEFAULT_OPTS="
--ansi
--layout=default
--info=inline
--color bg+:-1,gutter:-1
--height=50%
--multi
--preview-window=right:50%
--preview-window=sharp
--preview-window=cycle
--preview '([[ -f {} ]] && (bat --style=numbers --color=always --line-range :500 {} || cat {})) || ([[ -d {} ]] && (tree -C {} | less)) || echo {} 2> /dev/null | head -200'
--prompt='λ -> '
--pointer='❯'
--marker='✓'"
# --bind 'ctrl-e:execute(nvim {} < /dev/tty > /dev/tty 2>&1)' > selected
# --bind 'ctrl-v:execute(code {+})'"
export FZF_DEFAULT_COMMAND='rg --files --no-ignore --hidden --follow -g "!{.git,node_modules}/*" 2> /dev/null'
export FZF_CTRL_T_COMMAND="$FZF_DEFAULT_COMMAND"
#########################################################################
# ASDF
#########################################################################
export ASDF_DATA_DIR=$HOME/.asdf
export PATH=$ASDF_DATA_DIR/shims:$PATH
#########################################################################
# ATUIN
#########################################################################
atuin-setup() {
  ! hash atuin && return
  # bindkey '^R' _atuin_search_widget
  eval "$(atuin init zsh)"
}
atuin-setup
#########################################################################
# FZF-GC-PROJECT
#########################################################################
# Thanks to sei40kr/zsh-fzf-gcloud

function fzf-gcloud-config-set-project() {
  local project="$(gcloud projects list |
    fzf --header-lines=1 --reverse |
    awk '{ print $1 }')"
  if [[ -n "$project" ]]; then
    gcloud config set project "$project"
  fi
}
zle -N fzf-gcloud-config-set-project
bindkey '^G' fzf-gcloud-config-set-project
#########################################################################
# NIGHT DAY TOGGLE
#########################################################################
toggle_day_night() {
  NVIM_FILE="$HOME/.nv-ide/lua/plugins/colorschemes/tokyonight.lua"
  NVIM_LINE=10
  GHOSTTY_FILE="$HOME/.dot_files/core/ghostty/config"
  GHOSTTY_LINE=2

  if grep -q 'style = "day"' "$NVIM_FILE"; then
    sed -i "${NVIM_LINE}s/style = \"day\"/style = \"night\"/" "$NVIM_FILE"
    sed -i "${GHOSTTY_LINE}s/TokyoNight Day/TokyoNight Night/" "$GHOSTTY_FILE"
    echo "Switched to night style/theme."
  elif grep -q 'style = "night"' "$NVIM_FILE"; then
    sed -i "${NVIM_LINE}s/style = \"night\"/style = \"day\"/" "$NVIM_FILE"
    sed -i "${GHOSTTY_LINE}s/TokyoNight Night/TokyoNight Day/" "$GHOSTTY_FILE"
    echo "Switched to day style/theme."
  else
    echo "No 'style = \"day\"' or 'style = \"night\"' found on line $NVIM_LINE of $NVIM_FILE."
    return 1
  fi

  if command -v ghostty >/dev/null 2>&1; then
    pkill -SIGUSR2 ghostty
  else
    echo "Ghostty reload command not found. Please reload manually."
  fi
}
#########################################################################
# PATH
#########################################################################
export PATH=$PATH:/usr/local/go/bin:~/.local/bin:~/bin
#########################################################################
# GOOGLE-SDK
#########################################################################
if [ -f '/opt/google-cloud-sdk/path.zsh.inc' ]; then . '/opt/google-cloud-sdk/path.zsh.inc'; fi
#########################################################################
# GO SETTINGS
#########################################################################
export GOPATH=$HOME/Dev/go
#########################################################################
# FLUTTER SETTINGS
#########################################################################
export PATH="$PATH:$HOME/Dev/flutter/bin"
#########################################################################
# ANDROID STUDIO SETTINGS
#########################################################################
export PATH="$PATH:/opt/android-studio/bin"
#########################################################################
# MSSQL-TOOLS
#########################################################################
export PATH="$PATH:/opt/mssql-tools18/bin"
#########################################################################
# CARGO
#########################################################################
. "$HOME/.cargo/env"
#########################################################################
# YARN
#########################################################################
# export PATH="$HOME/.yarn/bin:$HOME/.config/yarn/global/node_modules/.bin:$PATH"
#########################################################################
# P10K SETTINGS
#########################################################################
[[ ! -f ~/.p10k.zsh ]] || source ~/.p10k.zsh
